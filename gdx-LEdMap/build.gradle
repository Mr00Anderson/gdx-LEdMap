plugins {
    id 'maven-publish'
}

publishing {
    publications {
        gdxLEdMap(MavenPublication) {
            pom {
                name = "gdx-LEdMap"
                description = "Libgdx library to implement LEd based tile maps."
                url = "https://github.com/lyze237/gdx-LEdMap"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/lyze237/gdx-LEdMap"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    api "com.badlogicgames.gdx:gdx:$gdxVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

test {
    testLogging {
        exceptionFormat = "full"
        showStackTraces = true
        showExceptions = true
        showCauses = true
        displayGranularity = 0
        events = [ "standardOut", "standardError", "started", "passed", "skipped", "failed" ]
        showStandardStreams = true
    }

    reports {
        junitXml.enabled = true
    }

    useJUnitPlatform()
}

task currentVersion() {
    doLast {
        new File(project.buildDir.path, "version.txt").text = project.version
    }
}
